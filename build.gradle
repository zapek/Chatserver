/*
 * Copyright (c) 2021 by David Gerber - https://zapek.com
 *
 * This file is part of Chatserver.
 *
 * Chatserver is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Chatserver is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with Chatserver.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.time.Instant

plugins {
    id 'org.springframework.boot' version '2.4.2' apply false
    id "com.github.node-gradle.node" version "2.2.4" apply false
    id 'org.ec4j.editorconfig' version '0.0.3' apply false
}

// To upgrade Gradle, change the version here and run the 'wrapper' task
wrapper {
    gradleVersion = '6.8.1'
}

task buildRetroshareImageForceCheckout(type:Exec) {
    workingDir "${project.rootDir}"
    commandLine "docker", "build", "-t", "retroshare", "--build-arg", "KEEP_SOURCE=true", "--build-arg", "FRESHCLONE=" + Instant.now(), "retroshare"
}

task buildRetroshareImage(type:Exec) {
    workingDir "${project.rootDir}"
    commandLine "docker", "build", "-t", "retroshare", "--build-arg", "KEEP_SOURCE=true", "retroshare"
}

configure(subprojects - project(":chatserver-frontend")) {
    group = 'com.zapek'
    version = '0.1.3'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'org.ec4j.editorconfig'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    check.dependsOn editorconfigCheck
}